#
# Copyright (C) Troy D. Straszheim
#
# Distributed under the Ryppl Software License, Version 1.0.
# See accompanying file LICENSE_1_0.txt or copy at
#   http://www.ryppl.org/LICENSE_1_0.txt
#

#
# This file is generated by cmake and installed to 
# RYPPL_VERSIONED_CMAKE_FILES_DIR/Ryppl-@RYPPL_VERSION@.
# 
# when a users does a find_package(Ryppl ...) RypplConfigVersion.cmake
# and RypplVersion.cmake will search for this file.
#

#
#  Various variables
#
set(Ryppl_INCLUDE_DIRS "@CMAKE_INSTALL_PREFIX@/@RYPPL_INCLUDE_INSTALL_DIR@")
set(Ryppl_INCLUDE_DIR "@CMAKE_INSTALL_PREFIX@/@RYPPL_INCLUDE_INSTALL_DIR@"
  CACHE FILEPATH "Ryppl include directory")
set(Ryppl_LIBRARY_DIRS "@CMAKE_INSTALL_PREFIX@/@RYPPL_LIB_INSTALL_DIR@")
#
#  Include the imported targets
#
include("@CMAKE_INSTALL_PREFIX@/@RYPPL_INSTALL_EXPORTS_FILE@/Ryppl.cmake")

#
#  Calculate target suffix from user variables
#
if (NOT DEFINED Ryppl_USE_MULTITHREADED)
  set(Ryppl_USE_MULTITHREADED TRUE)
endif()


if (Ryppl_USE_MULTITHREADED)
  set(mtflag "-mt")
  set(mtdesc "multi-threaded")
else()
  set(mtflag "")
  set(mtdesc "single-threaded")
endif()

if (Ryppl_USE_STATIC)
  set(staticflag "-static")
  set(staticdesc "statically-linked")
else()
  set(staticflag "-shared")
  set(staticdesc "dynamically-linked")
endif()

set(Ryppl_LIBRARIES "")
foreach(component ${Ryppl_FIND_COMPONENTS})
  #
  # Check that it is really a target
  # 
  set(target ryppl_${component}${mtflag}${staticflag})
  get_target_property(p ${target} TYPE)
  if (NOT (p MATCHES "_LIBRARY$"))
    message(STATUS "Library ryppl_${component} (${mtdesc}, ${staticdesc}) is not available in the detected Ryppl @RYPPL_VERSION@ installation under @CMAKE_INSTALL_PREFIX@.")
  else()
    list(APPEND Ryppl_LIBRARIES "optimized;${target}")
  endif()


  get_target_property(p ${target}-debug TYPE)
  if (NOT (p MATCHES "_LIBRARY$"))
    message(SEND_ERROR "Library ryppl_${component} (${mtdesc}, ${staticdesc}) is not available in the detected Ryppl @RYPPL_VERSION@ installation under @CMAKE_INSTALL_PREFIX@.")
  else()
    list(APPEND Ryppl_LIBRARIES "debug;${target}-debug")
  endif()

endforeach()

