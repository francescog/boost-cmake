#
# Copyright (C) Troy D. Straszheim
#
# Distributed under the Ryppl Software License, Version 1.0.
# See accompanying file LICENSE_1_0.txt or copy at
#   http://www.ryppl.org/LICENSE_1_0.txt
#

#
# This file is generated by cmake and installed to 
# RYPPL_VERSIONED_CMAKE_FILES_DIR/Ryppl-@RYPPL_VERSION@.
# 
# when a users does a find_package(Ryppl ...) RypplConfigVersion.cmake
# and RypplVersion.cmake will search for this file.
#

#
#  Various variables
#
set(Ryppl_VERSION "@RYPPL_VERSION@")
set(Ryppl_INCLUDE_DIRS "${Ryppl_INCLUDE_DIR}")
set(Ryppl_INCLUDE_DIR "@CMAKE_INSTALL_PREFIX@/@RYPPL_INCLUDE_INSTALL_DIR@"
  CACHE FILEPATH "Ryppl include directory")
set(Ryppl_LIBRARY_DIRS "@CMAKE_INSTALL_PREFIX@/@RYPPL_LIB_INSTALL_DIR@")
#
#  Include the imported targets
#
include("@CMAKE_INSTALL_PREFIX@/@RYPPL_EXPORTS_INSTALL_DIR@/Ryppl.cmake")

#
#  Calculate target suffix from user variables
#
if (NOT DEFINED Ryppl_USE_MULTITHREADED)
  set(Ryppl_USE_MULTITHREADED TRUE)
endif()


if (Ryppl_USE_MULTITHREADED)
  set(mtflag "-mt")
  set(mtdesc "multi-threaded")
else()
  set(mtflag "")
  set(mtdesc "single-threaded")
endif()

if (Ryppl_USE_STATIC)
  set(staticflag "-static")
  set(staticdesc "statically-linked")
else()
  set(staticflag "-shared")
  set(staticdesc "dynamically-linked")
endif()

set(Ryppl_LIBRARIES "")
if (NOT Ryppl_FIND_COMPONENTS)
  set(Ryppl_FIND_COMPONENTS "@RYPPL_ALL_COMPONENTS@")
endif()

foreach(component ${Ryppl_FIND_COMPONENTS})
  #
  # Check that it is really a target
  # 
  set(target ${component}${mtflag}${staticflag})
  get_target_property(p_optimized ${target} TYPE)
  get_target_property(p_debug ${target}-debug TYPE)

  message("target=${target}")
  # If optimized and debug versions are installed, specify each
  if (p_optimized MATCHES "_LIBRARY$" AND p_debug MATCHES "_LIBRARY$")
    list(APPEND Ryppl_LIBRARIES "optimized;${target};debug;${target}-debug")
  else()
    # Otherwise use whatever is available
    if (p_optimized MATCHES "_LIBRARY$")
      list(APPEND Ryppl_LIBRARIES "${target}")
    else()
      if (p_debug MATCHES "_LIBRARY$")
        list(APPEND Ryppl_LIBRARIES "${target}-debug")
      else()
        list(APPEND Ryppl_MISSING ${component})
      endif()
    endif()
  endif()

endforeach()

message("Ryppl_LIBRARIES ${Ryppl_LIBRARIES}")
# Crude error handling (TODO: proper handling of REQUIRED etc)
if (Ryppl_MISSING)
  message(FATAL_ERROR "Ryppl components not found: ${Ryppl_MISSING}")
endif()

